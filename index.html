<!DOCTYPE html>
<html lang="en">
    <!--
        Hello world!
        I love you Jadie!
    -->
    <head>
        <meta charset="UTF-8">
        <meta name="author" content="CJ Burkey">
        <meta name="description" content="My wife's favorite game, with her favorite things">
        <meta name="viewport" content="width=device-width, initial-scale=1.0">

        <title>Mah BB</title>

        <!--suppress CssUnusedSymbol -->
        <style>
            #options_dialog {
                display: flex;
                flex-direction: column;
                gap: 1rem;
                padding: 1rem;
                width: 30rem;
            }

            #options_dialog label {
                font-size: 1rem;
                text-align: right;
            }

            .input_group {
                display: grid;
                grid-template-columns: 1fr 1fr;
                gap: 1rem;
                align-items: center;
            }

            html, body, #wrapper {
                width: max-content;
                font-family: 'Arial', sans-serif;
            }

            #wrapper {
                overflow: hidden;
            }

            #game_container {
                display: flex;
                flex-direction: column;
                gap: 1rem;
                margin: 1rem;
                padding: 1rem;
                border: 1px solid black;
                width: max-content;
            }

            #game_board {
                display: grid;
                grid-template-columns: auto auto;
            }

            #board_top_row_columns {
                display: grid;
                text-align: center;
                align-items: center;
            }

            #board_left_column_rows {
                display: grid;
                text-align: center;
                align-items: center;
            }

            #board_grid {
                /* Prevent double tap to zoom in the grid area */
                touch-action: manipulation;
                display: grid;
                background-color: #AAAAAA;
                border: 2px solid #AAAAAA;
                gap: 2px;
            }

            .cell {
                background-color: white;
                position: relative;
            }

            .cell span {
                display: none;
                position: absolute;
                left: 0;
                top: 0;
                right: 0;
                bottom: 0;
                text-align: center;
                overflow: hidden;
                font-size: 1em;
                line-height: 2em;
                font-family: 'Arial', sans-serif;
                user-select: none;
            }

            .cell[data-black="true"] {
                background-color: black;
            }

            .cell[data-cross="true"] span {
                display: block;
            }

            .count_label {
                height: 2rem;
                vertical-align: middle;
                text-align: center;
                line-height: 2rem;
                font-family: monospace;
            }

            .padded {
                padding: 0.75rem;
            }
        </style>
    </head>
    <body>
        <h1>Mah BB</h1>
        <div id="wrapper">
            <div id="options_dialog" style="display: none;" _="on load remove @style from me">
                <div class="input_group">
                    <label for="size_select">Size:</label>
                    <select id="size_select">
                        <option value="10">10x10</option>
                        <option value="15" selected>15x15</option>
                        <option value="20">20x20</option>
                        <option value="25">25x25</option>
                        <option value="30">30x30</option>
                    </select>

                    <label for="url_select">Image URL:</label>
                    <input type="url" id="url_select" value="https://i.imgur.com/UniMfif.png">
                </div>

                <button id="start_btn" class="padded" _="on click add @disabled then send game_start to <body/>">Start game</button>
            </div>
            <div id="loading_dialog" _="on load hide me">
                <h3>Your experience awaits...</h3>
            </div>
        </div>

        <!--
            Remember to keep this up-to-date when necessary.
        -->
        <script src="https://unpkg.com/hyperscript.org@0.9.12"></script>

        <!-- Game event handling -->
        <script type="text/hyperscript">
            -- Handle game start
            on game_start
                -- Hide options dialog and show game board
                hide #options_dialog
                show #loading_dialog

                -- Get the board information
                set size to the value of #size_select

                -- Create the grid
                set game_container to create_game_board(size)
                if game_container exists
                    put game_container at the end of #wrapper
                end
            finally
                hide #loading_dialog
            end

            -- Handle returning to main menu
            on game_exit
                if #game_container exists
                    remove #game_container
                end
                set the *display of #options_dialog to 'flex'
                hide #loading_dialog
                remove @disabled from #start_btn
            end

            -- Called by the _ attribute on each cell with the reference to itself
            def cell_toggle(cell)
                if cell's @data-black
                    remove @data-black from cell
                    set cell's @data-cross to true
                else if cell's @data-cross
                    remove @data-cross from cell
                else
                    set cell's @data-black to true
                end
            end
        </script>

        <!-- Board creation -->
        <script type="text/hyperscript">
            def create_game_board(size, rows)
                -- EW EW EW
                if size mod 5 != 0 or size < 10 or size > 30
                    call alert('You sneaky devil, only sizes from 10-30 are allowed.')
                    send game_exit to <body/>
                    exit
                end

                -- Make the api call
                set api_values to make_api_request(size)
                if not api_values
                    send game_exit to <body/>
                    exit
                end

                set half_size to Math.ceil(size / 2.0)

                -- Create game container
                make a <div#game_container/> called game_container

                -- Create the return to menu button
                make a <button.padded/> then put it into game_container
                put 'Return to Menu' into it
                -- It's meta bro
                set its @_ to 'on click send game_exit to <body/>'

                -- Create the game board wrapping div
                make a <div#game_board/> called game_board then put it at the end of game_container

                -- Spacer
                make a <div/> then put it into game_board

                -- Top row column labels
                make a <div#board_top_row_columns/> called top_row then put it at the end of game_board
                set its @style to `grid-template-columns: repeat(${size}, 2em);`
                repeat half_size times index i
                    repeat size times index column
                        set col_counts to api_values["col_counts"][column]
                        set diff to half_size - col_counts.length
                        make a <div.count_label/> called label_at then put it at the end of top_row
                        if i >= diff
                            put col_counts[i - diff] into label_at
                        end
                    end
                end

                -- Left column row labels
                make a <div#board_left_column_rows/> called left_col then put it at the end of game_board
                set its @style to `grid-template-columns: repeat(${half_size}, 2em);`
                repeat size times index row
                    set row_counts to api_values["row_counts"][row]
                    set diff to half_size - row_counts.length
                    repeat half_size times index i
                        make a <div.count_label/> called label_at then put it at the end of left_col
                        if i >= diff
                            put row_counts[i - diff] into label_at
                        end
                    end
                end

                -- Board tiles
                make a <div#board_grid/> called board_grid_div then put it at the end of game_board
                set its @style to `grid-template-columns: repeat(${size}, 1fr);`
                repeat (size * size) times index i
                    make a <div.cell/> called cell then put it at the end of board_grid_div
                    set row to Math.floor(i / size)
                    set col to i mod size
                    set cell's @data-row to row
                    set cell's @data-col to col
                    set cell's @_ to `on click call cell_toggle(me)`
                    set cell's *display to 'flex'

                    set col_mod to col mod 5
                    set row_mod to row mod 5
                    if col_mod is 4
                        set cell's *border-right to '1px solid black'
                    else if col_mod is 0
                        set cell's *border-left to '1px solid black'
                    end
                    if row_mod is 4
                        set cell's *border-bottom to '1px solid black'
                    else if row_mod is 0
                        set cell's *border-top to '1px solid black'
                    end

                    make a <span/> called sp then put it into cell
                    put 'X' into sp
                end

                return game_container
            end

            def make_api_request(size)
                set safe_url to encodeURIComponent(the value of #url_select)
                set request to `https://cjburkey.com/mah-bb/api.php?size=${size}&url=${safe_url}`
                log `make request to: ${request}`

                fetch `${request}` as json
                set game_api_result to the result
                set error to game_api_result['error']
                set api_res to game_api_result['success']

                if error exists or not (api_res exists)
                    call alert(`An error occurred!\n${error}`)
                    exit
                end

                return api_res
            end
        </script>
    </body>
</html>
