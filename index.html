<!DOCTYPE html>
<html lang="en">
    <!--
        Hello world!
        I love you Jadie!
    -->
    <head>
        <meta charset="UTF-8">
        <meta name="author" content="CJ Burkey">
        <meta name="description" content="My wife's favorite game, with her favorite things">
        <meta name="viewport" content="width=device-width, initial-scale=1.0">

        <title>Mah BB</title>

        <style>
            #options_dialog > * {
                font-size: 1.1rem;
                margin: 0;
                padding: 0;
            }

            #options_dialog {
                display: flex;
                flex-direction: column;
                gap: 1rem;
                width: 10rem;
            }

            #options_dialog label {
                font-size: 1.5rem;
            }

            .padded {
                padding: 0.75rem;
            }

            /*noinspection CssUnusedSymbol*/
            .cell {
                aspect-ratio: 1/1;
            }
        </style>
    </head>
    <body>
        <h1>Mah BB</h1>
        <div id="wrapper">
            <div id="options_dialog">
                <label for="size_select">Size</label>
                <select id="size_select" class="padded">
                    <option value="10">10x10</option>
                    <option value="15">15x15</option>
                    <option value="20">20x20</option>
                </select>
                <button id="start_btn" class="padded" _="on click add @disabled then send game_start to <body/>">Start game</button>
            </div>
            <div id="loading_dialog" style="display: none;">
                <h3>Your experience awaits...</h3>
            </div>
        </div>

        <!--
            Using un-minified version for debugging (I think).
            Honestly it probably doesn't matter much in the end, but when
            not in development mode, I should eventually change this.
            Also, remember to keep this up-to-date when necessary.
        -->
        <script src="https://unpkg.com/hyperscript.org@0.9.12/dist/_hyperscript.js"></script>

        <!-- Game event handling -->
        <script type="text/hyperscript">
            -- Handle game start
            on game_start
                -- Hide options dialog and show game board
                hide #options_dialog
                show #loading_dialog

                -- Get the board information
                set size to the value of #size_select
                fetch `https://cjburkey.com/mah-bb/api.php?size=${size}` as json
                set game_api_result to the result
                set error to game_api_result['error']
                set api_res to game_api_result['success']

                if error exists or not (api_res exists)
                    call alert(`An error occurred!\n${error}`)
                    send game_exit to me
                    exit
                end

                -- TODO: DO SOMETHING WITH THIS

                -- Create the grid
                set game_container to create_game_board(size, rows)
                if game_container exists
                    put game_container at the end of #wrapper
                end
            finally
                hide #loading_dialog
            end

            -- Handle returning to main menu
            on game_exit
                if #game_container exists
                    remove #game_container
                end
                set the *display of #options_dialog to 'flex'
                remove @disabled from #start_btn
            end
        </script>

        <!-- Board creation -->
        <script type="text/hyperscript">
            def create_game_board(size, rows)
                -- EW EW EW
                if size != 10 and size != 15 and size != 20
                    call alert('You sneaky devil, only sizes 10, 15, and 20 are allowed.')
                    send game_exit to <body/>
                    exit
                end

                set half_size to Math.ceil(size / 2.0)

                -- Create game container
                make a <div#game_container/> called game_container

                -- Create the return to menu button
                make a <button.padded/> then put it into game_container
                put 'Return to Menu' into it
                set its @_ to 'on click send game_exit to <body/>'

                -- Create the game board wrapping div
                make a <div#game_board/> called game_board then put it at the end of game_container
                set its @style to `display: grid; grid-template-columns: ${half_size + 1}rem ${size * 2}rem;`

                -- Spacer
                make a <div/> then put it into game_board

                -- Top row column labels
                make a <div#board_top_row_columns/> called top_row then put it at the end of game_board
                set its @style to `display: grid; grid-template-columns: repeat(${size}, 1fr); text-align: center;`
                repeat half_size times index i
                    set ind to half_size - 1 - i
                    repeat size times index column
                        make a <span.column_count/> then put it at the end of top_row
                        set its @data-col to column
                        set its @data-index to ind
                    end
                end

                -- Left column row labels
                make a <div#board_left_column_rows/> called left_col then put it at the end of game_board
                set its @style to `display: grid; grid-template-columns: repeat(${half_size}, 1fr); text-align: center; align-items: center;`
                repeat size times index row
                    repeat half_size times index i
                        set ind to half_size - 1 - i
                        make a <span.column_count/> then put it at the end of left_col
                        set its @data-row to row
                        set its @data-index to ind
                    end
                end

                -- Board tiles
                make a <div#board_grid/> called board_grid_div then put it at the end of game_board
                set its @style to `display: grid; background-color: #AAAAAA; border: 2px solid #AAAAAA; gap: 2px; grid-template-columns: repeat(${size}, 1fr);`
                repeat (size * size) times index i
                    make a <div.cell/> then put it at the end of board_grid_div
                    set its @style to `background-color: white;`
                    set its @data-index to i
                end

                return game_container
            end
        </script>
    </body>
</html>
