<!DOCTYPE html>
<html lang="en">
    <!--
        Hello world!
        I love you Jadie!
    -->
    <head>
        <meta charset="UTF-8">
        <meta name="author" content="CJ Burkey">
        <meta name="description" content="My wife's favorite game, with her favorite things">
        <meta name="viewport" content="width=device-width, initial-scale=1.0">

        <title>Mah BB</title>

        <!--suppress CssUnusedSymbol -->
        <style>
            html, body {
                padding: 0;
                margin: 0;
                height: 100%;
            }

            body {
                min-width: 100%;
                width: max-content;
                display: flex;
                flex-direction: column;
            }

            #wrapper {
                flex-grow: 1;
                padding: 1rem;
                text-align: center;
                font-family: 'Arial', sans-serif;
            }

            #wrapper > h1 {
                padding: 0;
                margin: 0 0 1.0rem;
            }

            #wrapper > div {
                margin-left: auto;
                margin-right: auto;
            }

            footer {
                flex-shrink: 0;
                padding: 1rem;
                background-color: #EEEEEE;
                font-family: 'Arial', sans-serif;
            }

            footer a {
                color: #5555FF;
                text-decoration: none;
            }

            footer a:hover {
                color: #0505CC;
                text-decoration: underline;
            }

            #options_dialog {
                display: flex;
                flex-direction: column;
                max-width: 36rem;
                gap: 1rem;
            }

            .input_group {
                display: grid;
                grid-template-columns: 10rem auto;
                gap: 1rem;
                align-items: center;
            }

            .input_group label {
                font-size: 1rem;
                text-align: right;
            }

            /* Pretty much the only responsive part we really need ¯\_(ツ)_/¯ */
            @media (max-width: 400px) {
                .input_group label {
                    text-align: center;
                }

                /* Hack to add extra space between elements in here */
                .input_group > label:not(:nth-child(1)) {
                    margin-top: 1rem;
                }

                .input_group {
                    grid-template-columns: auto;
                    gap: 0.5rem;
                }
            }

            #deviant_search {
                border: 1px solid #CCCCCC;
                padding: 0.5rem;
                display: none;
                flex-direction: column;
                gap: 0.5rem;
            }

            #deviant_search_results {
                overflow: scroll;
                height: 10rem;
                display: flex;
                flex-direction: column;
                gap: 0.25rem;
            }

            #deviant_search_results > a {
                border: 1px solid black;
                border-radius: 4px;
                padding: 0.25rem;
                color: black;
                text-decoration: none;
            }

            #deviant_search_results > a[data-selected] {
                background-color: #AAAAFF;
            }

            #game_container {
                display: flex;
                flex-direction: column;
                gap: 1rem;
                padding: 1rem;
                border: 1px solid black;
                width: max-content;
            }

            #game_board {
                display: grid;
                grid-template-columns: auto auto;
            }

            #board_top_row_columns {
                display: grid;
                text-align: center;
                align-items: center;
            }

            #board_left_column_rows {
                display: grid;
                text-align: center;
                align-items: center;
            }

            #board_grid {
                /* Prevent double tap to zoom in the grid area */
                touch-action: manipulation;
                display: grid;
                background-color: #AAAAAA;
                border: 2px solid #AAAAAA;
                gap: 2px;
                position: relative;
            }

            #board_grid > .overlay_image {
                image-rendering: pixelated;
                object-fit: contain;
                position: absolute;
                width: 100%;
                height: 100%;
            }

            .cell {
                background-color: white;
                position: relative;
            }

            .cell span {
                display: none;
                position: absolute;
                left: 0;
                top: 0;
                right: 0;
                bottom: 0;
                text-align: center;
                overflow: hidden;
                font-size: 1em;
                line-height: 2em;
                font-family: 'Arial', sans-serif;
                user-select: none;
            }

            .cell[data-black="true"] {
                background-color: black;
            }

            .cell[data-cross="true"] span {
                display: block;
            }

            .count_label {
                height: 2rem;
                vertical-align: middle;
                text-align: center;
                line-height: 2rem;
                font-family: monospace;
            }

            .padded {
                padding: 0.75rem;
            }
        </style>
    </head>
    <body>
        <div id="wrapper">
            <h1>Mah BB</h1>
            <div id="options_dialog" style="display: none;" _="on load remove @style from me">
                <div class="input_group">
                    <!-- Save -->
                    <label for="size_select">Size:</label>
                    <select id="size_select">
                        <option value="10">10x10</option>
                        <option value="15" selected>15x15</option>
                        <option value="20">20x20</option>
                        <option value="25">25x25</option>
                        <option value="30">30x30</option>
                    </select>

                    <!-- DeviantArt Checkbox -->
                    <label for="use_deviations">DeviantArt Search?</label>
                    <input type="checkbox" id="use_deviations" style="justify-self: flex-start;" _="on change send update_search_display to <body/>">

                    <!-- DeviantArt Seach -->
                    <label id="search_label" for="deviant_search" style="display: none;">Image Search:</label>
                    <div id="deviant_search">
                        <input type="text" id="deviant_search_text" placeholder="Search term" _="on keyup debounced at 500ms send update_search_text to <body/>">
                        <div id="deviant_search_results"></div>
                    </div>

                    <!-- Image URL -->
                    <label for="url_select">Image URL:</label>
                    <input type="url" id="url_select" placeholder="https://" value="https://i.imgur.com/UniMfif.png">
                </div>

                <button id="start_btn" class="padded" _="on click add @disabled then add @disabled to #size_select then send game_start to <body/>">Start game</button>
            </div>
            <div id="loading_dialog" _="on load hide me">
                <h3>Your experience awaits...</h3>
            </div>
        </div>
        <footer>
            A game for my wife.
            <br>
            Made by hand with
            <a href="https://hyperscript.org/" target="_blank" title="Hyperscript :)">_hyperscript</a>
            by
            <a href="https://cjburkey.com" target="_blank" title="My website :)">CJ Burkey</a>
            <br>
            Source available on <a href="https://github.com/cjburkey01/mah-bb" target="_blank" title="mah-bb on GitHub">GitHub</a>
        </footer>

        <!--
            Remember to keep this up-to-date when necessary.
        -->
        <script src="https://unpkg.com/hyperscript.org@0.9.12"></script>

        <!-- Game event handling -->
        <script type="text/hyperscript">
            -- Automatically check the box if the browser has pre-checked this box
            -- for the the user
            on load
                send update_search_display
            end

            -- Handle showing/hiding the DeviantArt search is enabled/disabled
            -- WHY NO WORK
            on update_search_display
                if <#use_deviations:checked/> is empty
                    hide #deviant_search with *display
                    hide #search_label with *display
                    remove @disabled from #url_select
                else
                    set *display of #deviant_search to 'flex'
                    show #search_label with *display
                    add @disabled to #url_select
                end
            end

            on update_search_text
                set query to the value of #deviant_search_text
                set innerText of #deviant_search_results to ''
                fetch `https://cjburkey.com/mah-bb/deviant.php?q=${query}` as json
                repeat for item in the result
                    make a <a.deviation/> called link
                    set link's @href to '#'
                    set link's @_ to `on click remove @data-selected from .deviation then add @data-selected to me then set the value of #url_select to '${item['content']}' then halt`
                    put item['title'] into link
                    put link at the end of #deviant_search_results
                end
            end

            -- Handle game start
            on game_start
                -- Hide options dialog and show game board
                hide #options_dialog
                show #loading_dialog

                -- Get the board information
                set size to the value of #size_select

                -- Create the grid
                set game_container to create_game_board(size)
                if game_container exists
                    put game_container at the end of #wrapper
                end
            finally
                hide #loading_dialog
            end

            -- Handle showing the images when the game has been completed
            on game_complete
                set *pointer-events of #color_image to 'auto'
                set *pointer-events of #final_image to 'auto'
                transition #color_image's *opacity to '100%' over 1s
                wait 1s
                transition #final_image's *opacity to '100%' over 2s
            end

            -- Handle returning to main menu
            on game_exit
                if #game_container exists
                    remove #game_container
                end
                set the *display of #options_dialog to 'flex'
                hide #loading_dialog
                remove @disabled from #start_btn
                remove @disabled from #size_select
            end

            -- Called by the _hyperscript attribute on each cell
            def cell_toggle(cell)
                if cell's @data-black
                    remove @data-black from cell
                    set cell's @data-cross to true
                else if cell's @data-cross
                    remove @data-cross from cell
                else
                    set cell's @data-black to true
                end

                -- TODO: DON'T DO THIS EVERY CHANGE?
                send check_board to <body/>
            end

            def compare_arrays(arr1, arr2)
                if arr1.length == arr2.length
                    repeat for expected in arr1 index i
                        if expected != arr2[i]
                            return false
                        end
                    end
                    return true
                end
                return false
            end
        </script>

        <!-- Board creation -->
        <script type="text/hyperscript">
            def create_game_board(size, rows)
                if size mod 5 != 0 or size < 10 or size > 30
                    call alert('You sneaky devil, only sizes from 10-30 are allowed.')
                    send game_exit to <body/>
                    exit
                end

                -- Make the api call
                set api_values to make_api_request(size)
                if not api_values
                    send game_exit to <body/>
                    exit
                end

                -- Create game container
                make a <div#game_container/> called game_container

                -- Create the return to menu button
                make a <button.padded/> then put it into game_container
                put 'Return to Menu' into it
                set its @_ to 'on click send game_exit to <body/>'

                -- Create the game board wrapping div
                make a <div#game_board/> called game_board then put it at the end of game_container

                -- Spacer
                make a <div/> then put it into game_board

                set half_size to Math.ceil(size / 2.0)

                -- Top row column labels
                make a <div#board_top_row_columns/> called top_row then put it at the end of game_board
                set its @style to `grid-template-columns: repeat(${size}, 2em);`
                set max_col_nums to 0
                repeat for val in api_values["col_counts"]
                    if val.length > max_col_nums
                        set max_col_nums to val.length
                    end
                end
                repeat max_col_nums times index i
                    repeat size times index column
                        set col_counts to api_values["col_counts"][column]
                        set diff to max_col_nums - col_counts.length
                        make a <div.count_label/> called label_at then put it at the end of top_row
                        if i >= diff
                            set index to i - diff
                            put col_counts[i - diff] into label_at
                            set label_at's @data-index to index
                            set label_at's @data-col to column
                        end
                    end
                end

                -- Left column row labels
                set max_row_nums to 0
                repeat for val in api_values["row_counts"]
                    if val.length > max_row_nums
                        set max_row_nums to val.length
                    end
                end
                make a <div#board_left_column_rows/> called left_col then put it at the end of game_board
                set its @style to `grid-template-columns: repeat(${max_row_nums}, 2em);`
                repeat size times index row
                    set row_counts to api_values["row_counts"][row]
                    set diff to max_row_nums - row_counts.length
                    repeat max_row_nums times index i
                        make a <div.count_label/> called label_at then put it at the end of left_col
                        if i >= diff
                            set index to i - diff
                            put row_counts[index] into label_at
                            set label_at's @data-index to index
                            set label_at's @data-row to row
                        end
                    end
                end

                -- Board tiles
                make a <div#board_grid/> called board_grid_div then put it at the end of game_board
                set its @style to `grid-template-columns: repeat(${size}, 1fr);`
                repeat (size * size) times index i
                    make a <div.cell/> called cell then put it at the end of board_grid_div
                    set row to Math.floor(i / size)
                    set col to i mod size
                    set cell's @data-row to row
                    set cell's @data-col to col
                    set cell's @_ to `on click call cell_toggle(me)`
                    set cell's *display to 'flex'

                    set col_mod to col mod 5
                    set row_mod to row mod 5
                    if col_mod is 4
                        set cell's *border-right to '1px solid black'
                    else if col_mod is 0
                        set cell's *border-left to '1px solid black'
                    end
                    if row_mod is 4
                        set cell's *border-bottom to '1px solid black'
                    else if row_mod is 0
                        set cell's *border-top to '1px solid black'
                    end

                    make a <span/> called sp then put it into cell
                    put 'X' into sp
                end

                -- Pixelated version of the original image
                set color_image to create_color_image_element(size)
                put color_image at the end of board_grid_div
                hide color_image with opacity

                -- The actual original image
                set final_image to create_final_image_element(size)
                put final_image at the end of board_grid_div
                hide final_image with opacity

                return game_container
            end

            def create_color_image_element(size)
                set safe_url to encodeURIComponent(the value of #url_select)
                set url to `https://cjburkey.com/mah-bb/image.php?size=${size}&process=0&url=${safe_url}`

                make an <img.overlay_image#color_image/> called color_image
                set color_image's @src to url
                set color_image's @alt to 'Pixelated Color Image'
                set color_image's *pointer-events to 'none'
                return color_image
            end

            def create_final_image_element(size)
                set safe_url to encodeURIComponent(the value of #url_select)
                set url to `https://cjburkey.com/mah-bb/image.php?size=${size}&process=0&scale=0&url=${safe_url}`

                make an <img.overlay_image#final_image/> called final_image
                set final_image's @src to url
                set final_image's @alt to 'Original Image'
                set final_image's *pointer-events to 'none'
                return final_image
            end

            def make_api_request(size)
                set safe_url to encodeURIComponent(the value of #url_select)
                set request to `https://cjburkey.com/mah-bb/api.php?size=${size}&url=${safe_url}`
                log `make request to: ${request}`

                fetch `${request}` as json
                set game_api_result to the result
                set error to game_api_result["error"]
                set api_res to game_api_result["success"]

                if error exists or not (api_res exists)
                    call alert(`An error occurred!\n${error}`)
                    exit
                end

                return api_res
            end
        </script>

        <!-- Completion checking -->
        <script type="text/hyperscript">
            on check_board
                -- Check if the row run counts match
                set size to the value of #size_select

                repeat size times index row
                    set run to 0
                    set runs to []
                    repeat size times index col
                        if @data-black of the first <.cell[data-row=`${row}`][data-col=`${col}`]/>
                            increment run
                        else
                            if run > 0
                                append run to runs
                                set run to 0
                            end
                        end
                    end
                    if run > 0
                        append run to runs
                        set run to 0
                    end

                    set raw_expected_runs to <.count_label[data-row=`${row}`]/>
                    set len to raw_expected_runs.length

                    set expected_runs to []
                    if len > 0
                        repeat len times index i
                            set run_count_at to the innerText of the first <.count_label[data-row=`${row}`][data-index=`${i}`]/>
                            append run_count_at as Int to expected_runs
                        end
                    end

                    if not compare_arrays(expected_runs, runs)
                        log `mismatch on row ${row}`
                        exit
                    end
                end

                -- Check if the column run counts match
                set size to the value of #size_select
                repeat size times index col
                    set run to 0
                    set runs to []
                    repeat size times index row
                        if @data-black of the first <.cell[data-row=`${row}`][data-col=`${col}`]/>
                            increment run
                        else
                            if run > 0
                                append run to runs
                                set run to 0
                            end
                        end
                    end
                    if run > 0
                        append run to runs
                    end

                    set raw_expected_runs to <.count_label[data-col=`${col}`]/>
                    set len to raw_expected_runs.length

                    set expected_runs to []
                    if len > 0
                        repeat len times index i
                            set run_count_at to the innerText of the first <.count_label[data-col=`${col}`][data-index=`${i}`]/>
                            append run_count_at as Int to expected_runs
                        end
                    end

                    if not compare_arrays(expected_runs, runs)
                        log `mismatch on column ${col}`
                        exit
                    end
                end

                log 'game complete'
                send game_complete to <body/>
            end
        </script>
    </body>
</html>
